//Barrel spawner
class SF_BarrelSpawner : RandomSpawner
{
	Default
	{
		DropItem "SF_Barrel", 255, 3;
		DropItem "SF_EmptyBarrel", 255, 1;
		DropItem "SF_FireBarrel", 255, 1;
	}
}

//Barrel
class SF_Barrel : ExplosiveBarrel
{
	bool variant;

	Default { Height 28; }
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_JumpIf(variant, "Spawn2");
			SBAR ABCB 10;
			Goto Spawn + 1;
		
		Spawn2:
			SBA5 ABCB 10;
			Loop;
		
		Death:
			TNT1 A 0 A_JumpIf(variant, "Death2");
			SBAR C 5;
			SBAR D 5 Bright A_Scream;
			SBAR E 5 Bright A_SpawnDebris("SF_Debris");
			SBAR F 5 Bright A_Explode();
			SBAR G 5 Bright;
			TNT1 A 1055 A_BarrelDestroy;
			TNT1 A 5 A_Respawn;
			Wait;
		
		Death2:
			SBA5 C 5;
			SBA5 D 5 Bright A_Scream;
			Goto Death + 3;
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		variant = RandomPick(true, false);
	}
}

//Empty barrel
class SF_EmptyBarrel : SF_Barrel
{
	Default { Health 15; }
	States
	{
		Spawn:
			TNT1 A 0 NoDelay { Soulful.SpawnGrass(self); }
			SBA2 A -1 A_Jump(128, 1);
			Stop;
			SBA2 B -1 A_Jump(128, "Spawn2");
			Stop;
		
		Spawn2:
			TNT1 A 0 A_SetSize(radius, 20);
			SBA3 A -1 A_Jump(128, 1);
			Stop;
			SBA3 B -1 A_Jump(128, 1);
			Stop;
			SBA3 C -1 A_Jump(128, 1);
			Stop;
			SBA3 D -1;
			Stop;
		
		Death:
			TNT1 A 0 Bright
			{
				int item = Random(0, 6);
				class<Actor> actor;
				switch (item)
				{
					case (0): actor = "HealthBonus"; break;
					case (1): actor = "ArmorBonus"; break;
					case (2): actor = "Stimpack"; break;
					case (3): actor = "Clip"; break;
					case (4): actor = "Shell"; break;
					case (5): actor = "RocketAmmo"; break;
					case (6): actor = "Cell"; break;
				}
				A_SpawnItemEx(actor);
				A_NoBlocking();
				A_SpawnDebris("SF_Debris");
			}
			TNT1 A 1080 A_BarrelDestroy;
			TNT1 A 5 A_Respawn;
			Wait;
	}
}

//Fire barrel
class SF_FireBarrel : SF_Barrel
{
	States
	{
		Spawn:
			SBA4 A -1;
			Stop;
		
		Death:
			SBA4 A 5;
			SBA4 A 5 Bright A_Scream;
			SBA4 B 5 Bright;
			SBA4 C 5 Bright
			{
				for (int i = 0; i < 8; i++)
				{
					double angle = i * 45;
					A_SpawnItemEx("SF_Fire", 0, 0, 0, sin(angle) * 5, cos(angle) * 5, 5);
				}
				A_StartSound("SF_FireBarrel/fire");
			}
			SBA4 D 5 Bright;
			TNT1 A 1055 A_BarrelDestroy;
			TNT1 A 5 A_Respawn;
			Wait;
	}
}