// Health Bonus Spawner
class SF_HealthBonusSpawner : SF_Spawner {
	States {
		Spawn:
			TNT1 A 0 NoDelay A_JumpIf((curSector && curSector.IsSecret()) || Random(0, 3) == 0, 2);
			TNT1 A 0 A_Spawner("SF_HealthBonus");
			Stop;
			TNT1 A 0 A_Spawner("SF_SuperHealthBonus");
			Stop;
	}
}

// Health Bonus
class SF_HealthBonus : HealthBonus {}

// Super Health Bonus
class SF_SuperHealthBonus : HealthBonus {
	Default {
		Inventory.Amount 2;
		Inventory.MaxAmount 300;
		Inventory.PickupMessage "Picked up a super health bonus.";
	}
	
	States {
		Spawn:
			SSHB ABCDCB 6;
			Loop;
	}
}

// Berserk Spawner
class SF_BerserkSpawner : RandomSpawner {
	Default {
		DropItem "SF_Berserk", 255, 1;
		DropItem "SF_QuadDamageSphere", 255, 1;
		DropItem "SF_AndrenalinePack", 255, 1;
	}
}

// Berserk
class SF_Berserk : Berserk {}

// Quad Damage Sphere
class SF_QuadDamageSphere : Berserk {
	Default {
		Inventory.PickUpMessage "Quadruple Damage!";
	}

	States {
		Spawn:
			SQDS AB 6 Bright;
			Loop;

		Pickup:
			TNT1 A 0 A_GiveInventory("SF_PowerQuadDamage");
			Stop;
	}
}

// Andrenaline Pack
class SF_AndrenalinePack : Berserk {
	Default {
		Inventory.PickUpMessage "Andrenaline Rush!";
	}
	
	States {
		Spawn:
			SARK A -1 Bright;
			Stop;

		Pickup:
			TNT1 A 0 A_GiveInventory("SF_PowerAndrenaline");
			TNT1 A 0 A_GiveInventory("SF_PowerAndrenaline2");
			Stop;
	}
}