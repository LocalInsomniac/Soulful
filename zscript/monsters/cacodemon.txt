//Cacodemon Spawner
class SF_CacodemonSpawner : Actor
{
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		class<Actor> spawn = ((curSector && curSector.lightLevel <= 128 && Random(0, 8) == 0) || (Soulful.IsOnGrass(self) && Random(0, 16) == 0)) ? 'SF_Watcher' : 'SF_Cacodemon';
		A_SpawnItemEx(spawn, flags: SXF_TransferAmbushFlag | SXF_TransferPointers | SXF_TransferSpecial);
		Destroy();
	}
}

//Cacodemon
class SF_Cacodemon : Cacodemon
{
	Default { BloodColor "Blue"; }
	
	States
	{
		Spawn:
			SHEA A 10 A_Look;
			Loop;
		
		See:
			SHEA A 3 A_Chase;
			Loop;
		
		Missile:
			SHEA B 5 A_FaceTarget;
			SHEA C 5 A_FaceTarget;
			SHEA D 5 Bright A_HeadAttack;
			Goto See;
		
		Pain:
			SHEA E 3;
			SHEA E 3 A_Pain;
			SHEA F 6;
			Goto See;
		
		Death:
			SHEA G 8;
			SHEA H 8 A_Scream;
			SHEA I 8;
			SHEA J 8;
			SHEA J 1 A_JumpIf((pos.z == floorZ || bOnMObj), 1);
			Goto Death + 4;
			SHEA K 8 A_NoBlocking;
			SHEA L -1 A_SetFloorClip;
			Stop;
		
		Raise:
			SHEA L 8 A_UnSetFloorClip;
			SHEA KJIHG 8;
			Goto See;
	}
}

//Watcher
class SF_Watcher : Cacodemon
{
	Default
	{
		SeeSound "SF_Watcher/sight";
		ActiveSound "SF_Watcher/active";
		PainSound "SF_Watcher/pain";
		DeathSound "SF_Watcher/death";
		AttackSound "SF_Watcher/attack";
		Obituary "%o was stared by a Watcher.";
		MaxTargetRange 256;
	}
	
	States
	{
		Spawn:
			SWAT A 10 A_Look;
			Loop;
		
		See:
			SWAT A 3 A_Chase;
			SWAT A 0 A_JumpIf((Random(0, 9) == 0 && Distance3D(target) <= 960 && CheckSight(target)), "Strafe");
			Loop;
			
		Strafe:
			SWAT A 0 A_FaceTarget();
			SWAT A 1 A_ChangeVelocity(15, RandomPick(-15, 15), vel.z, CVF_Relative | CVF_Replace);
			SWAT AAAAAAAAAAAAAA 1 A_ChangeVelocity(vel.x * 0.93, vel.y * 0.93, vel.z, CVF_Replace);
			SWAT A 0 { vel = (0, 0, vel.z); }
			Goto See;
		
		Melee:
		Missile:
			SWAT B 4 Bright A_FaceTarget;
			SWAT C 4 Bright A_FaceTarget;
			SWAT DCDCDCDCD 2 Bright A_SFWatcherAttack;
			SWAT E 4 Bright;
			SWAT A 0 A_Jump(64, "Strafe");
			Goto See;
		
		Pain:
			SWAT A 3;
			SWAT A 3 A_Pain;
			SWAT A 0 { vel = (0, 0, vel.z); }
			SWAT A 0 A_JumpIf((Random(0, 3) == 0 && CheckSight(target)), "Strafe");
			Goto See;
		
		Death:
			SWAT F 5;
			SWAT G 5 A_Scream;
			SWAT H 5;
			SWAT I 5;
			SWAT J 5 A_NoBlocking;
			SWAT K 20 A_SetFloorClip;
			SWAT K 10 A_SFWatcherFart;
			SWAT K 30 A_SFWatcherFart2;
			SWAT K 10 A_SFWatcherFart;
			SWAT K 30 A_SFWatcherFart2;
			SWAT K 10 A_SFWatcherFart;
			SWAT K 30 A_SFWatcherFart2;
			SWAT K 10 A_SFWatcherFart;
			SWAT K 30 A_SFWatcherFart2;
			SWAT K 10 A_SFWatcherFart;
			SWAT K -1 A_SFWatcherFart2;
			Stop;
		
		Raise:
			SWAT K 5 A_UnsetFloorClip;
			SWAT JIHGF 5;
			Goto See;
	}
	
	action void A_SFWatcherAttack()
	{
		A_FaceTarget();
		A_SpawnProjectile("SF_WatcherGas", 36, 0, FRandom(-5, 5), CMF_OffsetPitch, FRandom(-5, 5));
	}
	
	action void A_SFWatcherFart() { A_SpawnItemEx("SF_WatcherFart", zofs: 16); }
	action void A_SFWatcherFart2() { Soulful.RadiusDamage(self, 128, 10, "Poison"); }
}

//Watcher gas
class SF_WatcherGas : Actor
{
	Default
	{
		+ThruActors
		+NoGravity
		Radius 3;
		Height 6;
		Speed 10;
		FastSpeed 15;
		RenderStyle "Add";
		Alpha 0.4;
		SeeSound "SF_Watcher/attack";
		BounceType "Grenade";
		BounceFactor 1;
	}
	
	States
	{
		Spawn:
			SGAS ABCDEFGHIJKLMNOP 2 Bright { Soulful.RadiusDamage(self, 50, 1, "Poison"); }
			Stop;
	}
}

//Watcher death gas
class SF_WatcherFart : Actor
{
	Default
	{
		+NoGravity
		RenderStyle "Add";
		Alpha 0.6;
		Scale 1.5;
	}
	
	States
	{
		Spawn:
			SGAS ABCDEFGHIJKLMNOP 3;
			Stop;
	}
	
	override void PostBeginPlay() { vel.z = 1; }
}