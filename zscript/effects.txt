// Barrel/Lost Soul debris
class SF_Debris : Actor {
	Default {
		+Corpse
		+NoTeleport
		+NoBlockMap
		+NotOnAutomap
		
		FloatBobPhase 0;
		Health 10;
		Radius 8;
		Height 1;
		Mass 1;
		Gravity 0.6;
	}
	
	States {
		Spawn:
			SDEB A 0 NoDelay A_Jump(128, 5);
			SDEB ABCD 3;
			Goto Spawn + 1;
			SDEB EFGH 3;
			Goto Spawn + 5;
		
		Crash:
			TNT1 A 0;
			Stop;
	}
}

// Fire
class SF_Fire : Actor {
	Default {
		+DropOff
		
		Radius 6;
		Height 16;
		Gravity 0.5;
		RenderStyle "Translucent";
		Obituary "%o was burnt to a crisp.";
		DamageType "Fire";
	}
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_Jump(128, "Spawn2");
			SFIR A 4 Bright A_SFFire();
			SFIR BC 4 Bright;
			Goto Spawn + 1;
		
		Spawn2:
			TNT1 A 0 A_Jump(128, "Spawn3");
			SFI2 A 4 Bright A_SFFire();
			SFI2 BCD 4 Bright;
			Goto Spawn2 + 1;
		
		Spawn3:
			SFI3 A 4 Bright A_SFFire();
			SFI3 BCD 4 Bright;
			Loop;
	}
	
	action void A_SFFire() {
		Soulful.RadiusDamage(self, 50, alpha * 10, "Fire");
	}
	
	override void Tick() {
		super.Tick();
		
		if (pos.z == floorZ) {
			vel.x = 0;
			vel.y = 0;
		}
		
		alpha -= 0.002;
		scale.x -= 0.0005;
		scale.y -= 0.0005;

		if (waterLevel > 0 || alpha <= 0) {
			Destroy();
		}
	}
}